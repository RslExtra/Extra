/**
 *  @package Collections
 *  Базовые классы и функции коллекций.
*/

import "..\\Mac\\Extra\\OOP\\OOP.mac";           // Any
import "..\\Mac\\Extra\\OOP\\Comparators.mac";
import "..\\Mac\\Extra\\OOP\\SortingFunc.mac";
import "..\\Mac\\Extra\\OOP\\Exceptions.mac";
import "..\\Mac\\Extra\\Utils\\StringUtils.mac";  // StringJoiner
import "..\\Mac\\Extra\\Utils\\SArray.mac";
import "..\\Mac\\Extra\\Lambda\\Lambda.mac";

import rcw;

/**
   Объект для Map.
*/
Class Pair(k, v)
   var key = k;
   var value = v;
   Macro toString() : String
      return "(" + key + " - " + value + ")";
   End;
End;

Private Macro ifNull(val : Variant, replace : Variant) : Variant
   if(Valtype(val) == V_UNDEF)
      return replace;
   end;
   return val;
End;

Private Class EmptyObj()
   Macro equals() : Bool
      return true;
   End;

   Macro compareTo() : Integer
      return 0;
   End;

   Macro hash()
      return 0;
   End;

   Macro toString() : String
      return "Empty";
   End;
End;

/** Нулевой объект-заглушка для [SetInterface]*/
Const PRESENT = EmptyObj();  //

/**
   Интерфейс базового итератора для всех коллекций,
   методы обязательны для переопределения
*/
Class IteratorInterface(collectionInstance)

   /** Возвращает следующий элемент коллекции или `null` если находится в конце списка*/
   Macro next() : Variant
   End;
   /** Возвращает `true` если имеется следующий элемент коллекции. `false`  в противном случае. */
   Macro hasNext() : Bool
   End;

   /** Возвращает предыдущий элемент коллекции или `null` если находится в начале списка*/
   Macro previous() : Variant
   End;

   /** Возвращает `true` если имеется предыдущий элемент коллекции. `false`  в противном случае. */
   Macro hasPrev() : Bool
   End;

   /** Повторно возвращает последний уже отданный элемент. */
   Macro getLastReturned() : Variant
   End;

   /** Переводит указатель итератора в начало списка. */
   Macro clear()
   End;
End;

/**
   Обратный итератор.
*/
Class DescendingIterator(collectionInstance, ind : Integer)
   private var inst = collectionInstance;
   private var itr = null;

   if (ValType(ind) == V_UNDEF)
      ind = 0;
   elif (ValType(ind) != V_INTEGER)
      Throw(IllegalArgumentException());
   elif ((ind < 0) or (ind > inst.size))
      Throw(IndexOutOfBoundsException());
   end;
   itr = inst.newIterator(inst.size-ind);

   Macro hasNext() : Bool
      return itr.hasPrev();
   End;

   Macro next() : Variant
      return itr.prev();
   End;

   Macro hasPrev() : Bool
      return itr.hasNext();
   End;

   Macro prev() : Variant
      return itr.next();
   End;

   Macro nextIndex() : Integer
      return itr.nextIndex - 1;
   End;

   Macro clear()
      itr.toPos(inst.size);
   End;

   Macro toString() : String
      return ("<-" + itr.toString());
   End;
End;

/**
   Обертка для [IteratorInterface]. Позволяет перебирать элементы коллекций (наследников [Iterable]) нативным `for(e, iterable)`;
*/
Class NativeIterator(_itr : IteratorInterface)
   var itr  = _itr;

   Macro next() : Bool
      if(itr.hasNext())
         itr.next;
         return true;
      end;
      return false;
   End;

   Macro item() : Variant
      return itr.getLastReturned();
   End;

   Macro toString() : String
      return String(itr);
   End;
End;

private var StrJoiner   = StringJoiner(", ", "[", "]");
private var StrJoinerML = StringJoiner("\n","[\n", "\n]");
/**
   Базовый класс коллекций. Содержит описание интерфейса коллекций.
*/
Class Iterable()
   /** Размер коллекции.*/
   var size;

   /** Возвращает новый экземпляр текущей коллекции с теми  же компараторами и функциями проверки на равенство. */
   Macro newInstance()
   End;

   /** Удаляет все элементы коллекции. */
   Macro clear()
   End;

   /** Возвращает `true`, если коллекция пуста, иначе возвращает `false`.*/
   Macro isEmpty() : Bool
      return (size == 0);
   End;

   /**
      Возвращает массив типа [Utils.SArray SArray] состоящий из элементов исходной коллекции.
   */
   Macro toArray() : SArray
   End;

   /** Добавлет базовый элемент коллекции, для [ListInterface списков] и [SetInterface множеств] это Variant элементы, для [MapInterface карт] это объект типа [Pair] с полями key и value. Для внутреннего использования. */
   Macro append(baseCollectionElement)
   End;


   /** Возвращает новый итератор начинающий с позиции 'ind'. Если 'ind' не задан или равен `null` итератор пойдет с начала коллекции. */
   Macro newIterator(ind) : IteratorInterface
   End;

   /**
      Возвращает быструю копию исходной коллекции. Каждый базовый элемент коллекции копируется по значению если примитив и по ссылке если объектного типа.
   */
   Macro copy() : Iterable
      var result = newInstance();
      var itr = newIterator();
      while (itr.hasNext())
         result.append(itr.next);
      end;
      return result;
   End;

   // Chain - функции

   /**
      Возвращает объект того же типа коллекции с элементами отфильтрованными согласно функции предикату 'predicate'

      @param predicate - ссылка на функцию-предикат вида Macro func(el : Variant) : Bool

   */
   Macro filter(predicate) : Iterable
      var func = Lambdas.getFunc(predicate);
      var result = newInstance();
      var itr = newIterator();
      while (itr.hasNext())
         var el = itr.next;
         if (ExecMacro2(func, el))
            result.append(el);
         end;
      end;
      return result;
   End;

   /**
      Возвращает объект того же типа коллекции с элементами преобразованными функцией 'mapfunc'.

      @param mapfunc - ссылка на функцию вида Macro func(el : Variant) : Variant
   */
   Macro map(mapfunc) : Iterable
      var func = Lambdas.getFunc(mapfunc);
      var result = newInstance();
      var el, outEl;
      var itr = newIterator();
      while (itr.hasNext())
         el = itr.next;
         outEl = ExecMacro2(func, el);
         result.append(outEl);
      end;
      return result;
   End;

   /**
      Вызывает 'func' для каждого элемента массива, возвращает `null`.
      Пример:

      @sample [samples.sArray.forEach]
   */
   Macro forEach(func)
      func = Lambdas.getFunc(func);
      var itr = newIterator();
      while (itr.hasNext)
         ExecMacro(func, itr.next());
      end;
   End;

   /**
      Возвращает результат агрегатной функции 'accumulator' на элементы исходного массива.
      'identity' - начальное значение.

   */
   Macro reduce(identity, accumulator) : Variant
      var func = Lambdas.getFunc(accumulator);
      var itr = newIterator();
      while (itr.hasNext)
         identity = ExecMacro2(accumulator, identity, itr.next());
      end;
      return identity;
   End;

   /**
      Возвращает сумму элементов коллекции (только для примитивов)
   */
   Macro sum()
      var sum = 0;
      var itr = newIterator();
      while (itr.hasnext())
         sum = sum + itr.next();
      end;
      return sum;
   end;

   /**
      Возвращает сумму элементов коллекции по имени свойства объектов.
      _propName - имя свойство объекта
   */
   Macro sumByProp(_propName : String) : Numeric
      var itr = newIterator();
      var v, p, sum = 0;
      while (itr.hasnext())
         v = itr.next();
         p = GenGetProp(v, _propName);
         sum = sum + p;
      end;
      return sum;
   End;

   /**
      Возвращает сумму преобразованных элементов. Аналог `.map(@func).sum();`
      @param _func - ссылка на функцию - преобразователь вида Macro func(el : Variant) : Numeric
   */
   Macro sumBy(_func)
      var func = Lambdas.getFunc(_func);
      var sum = 0;
      var itr = newIterator();
      var p;
      while (itr.hasnext())
         p = ExecMacro2(func, itr.next());
         sum = sum + p;
      end;
      return sum;
   End;


   /**
      Возвращает строку из элементов коллекции, разделенных '_delim', начинается с '_prefix' заканчивается '_postfix'.

      @oparam _delim = "," разделитель
      @oparam _prefix = "[" префикс вывода
      @oparam _postfix = "]" постфикс вывода

   */
   Macro join(_delim, _prefix, _postfix) : String
      var delim   = ifNull(_delim, ", ");
      var prefix  = ifNull(_prefix, "[");
      var postfix = ifNull(_postfix,"]");
      return StringJoiner(delim, prefix, postfix).joinToString(this);
   End;

   /** Выводит коллекцию на экран без переноса строк*/
   Macro toScreen()
      StrJoiner.joinToPrint(this);
   End;

   /** Выводит коллекцию на экран с переносом строк*/
   Macro toScreen2()
      StrJoinerML.joinToPrint(this);
   End;

   /**
      Метод для расчета хэша коллекции. Хотя он и реализован, не рекомендуется использовать коллекцию как ключ в [MapInterface].
   */
   Macro hash() : Integer
      var h = 1;
      for (var el, this)
         h = h*17 + VariantHashCode(el);
      end;
      return abs(h);
   End;

   /**
      Метод для проверки на равенство текущей коллекции и 'otherIterable'. Две коллекции будут равны, если они одного типа, размера и попарно равны все их элементы.
   */
   Macro equals(otherIterable) : Bool
      // Inherited implementation
   End;


   /**
      Создает [StreamApi.BornStream Stream] коллекции.
   */
   Macro toStream()
      var streamObj = ExecMacro2("StreamOf", this);
      if (streamObj == null)
         Throw(ModuleNotImportedException("Stream"));
      end;
      return streamObj;
   End;

   // Метод для возможности перечисления нативным for(el, collection)
   Macro createEnum()
      return NativeIterator(newIterator());
   End;
End;

/**
   Базовый интерфейс для списков.

   @see [Collections.ArrayList]
   @see [Collections.LinkedList]
   @see [Collections.SortedLinkedList]
*/
Class (Iterable) ListInterface()

   /** Ссылка на функцию компаратор. */
   private var comparator ;
   /** Ссылка на функцию проверки на равенство. */
   private var equalsfunc ;

   /**
      Устанавливает необходимые для работы с конкретным 'rslType' типом RSL 'comparator' и 'equalsFunc' и возвращает текущий экземпляр списка.
   */
   Macro setType(rslType) : ListInterface
      if ((rslType > V_UNDEF) and (rslType <= V_TIME))
         comparator = @CF_ComparePrimitive;
         equalsfunc = @CF_EqualsPrimitive;
      elif (rslType == V_GENOBJ)
         comparator = @CF_CompareToImpl;
         equalsfunc = @CF_EqualsImpl;
      else
         return Throw(IllegalArgumentException("Данный тип не предусмотрен реализацией." + rslType));
      end;
      return this;
   End;

   /**
      Устанавливает функцию-компаратор 'comparefun' и возвращает текущий список.
   */
   Macro setComparator(comparefun) : ListInterface
      if (ValType(compareFun) == V_STRING)
         comparator = Lambdas.getFunc(compareFun);
      end;
      return this;
   End;

   /**
      Устанавливает функцию проверки на равенство элементов списка 'equalsfun' и возвращает текущий список.
   */
   Macro setEqualsFunc(equalsfun) : ListInterface
      if (ValType(equalsfun) == V_STRING)
         equalsfunc = Lambdas.getFunc(equalsfun);
      end;
      equalsfunc = equalsfun;
      return this;
   End;

   /**
      Добавляет 'element' в список на позицию 'ind'. Если 'ind' не указан или равен `null`, добавляет элемент в конец списка.

      @Sample [Samples.Collections.List.add]
   */
   Macro add(element : Variant, ind : Integer)
   End;

   /**
      Добавляет все аргументы в конец списка и возвращает текущий экземпляр.

      @Sample [Samples.Collections.List.addItems]
   */
   Macro addItems(varargs )
   End;

   /**
      Добавляет 'element' в начало списка.

      @Sample [Samples.Collections.List.addFirst]
   */
   Macro addFirst(element : Variant)
   End;

   /**
      Добавляет 'element' в конец списка.

      @Sample [Samples.Collections.List.addLast]
   */
   Macro addLast(element : Variant)
   End;

   /**
      Добавляет все элементы 'enumerable' в конец списка.

      @Sample [Samples.Collections.List.addAll]
   */
   Macro addAll(enumerable, ind : Integer)
   End;

   /**
      Возвращает элемент под индексом 'ind'.

      @Sample [Samples.Collections.List.get]
   */
   Macro get(ind : Integer) : Variant
   End;

   /**
      Возвращает первый элемент списка.

      @Sample [Samples.Collections.List.getFirst]
   */
   Macro getFirst() : Variant
   End;

   /**
      Возвращает последний элемент списка.

      @Sample [Samples.Collections.List.getLast]
   */
   Macro getLast() : Variant
   End;

   /**
      Устанавливает новое значение 'newValue' элементу под индексом 'ind' и возвращает заменяемое значение.

      @Sample [Samples.Collections.List.set]
   */
   Macro set(ind : Integer, newValue : Variant) : Variant
   End;

   /**
      Удаляет и возвращает значение под индексом 'ind'.

      @Sample [Samples.Collections.List.remove]
   */
   Macro remove(ind : Integer) : Variant
   End;

   /**
      Удаляет и возвращает первый элемент.

      @Sample [Samples.Collections.List.removeFirst]
   */
   Macro removeFirst() : Variant
   End;

   /**
      Удаляет и возвращает последний элемент.

      @Sample [Samples.Collections.List.removeLast]
   */
   Macro removeLast() : Variant
   End;

   /**
      Удаляет все элементы из 'enumerable' и возвращает `true`, если был найден и удален хотябы один элемент.

      @Sample [Samples.Collections.List.removeAll]
   */
   Macro removeAll(enumerable) : Bool
   End;

   /**
      Возвращает индекс первого элемента равного 'element' или -1, если элемент не найден.

      @Sample [Samples.Collections.List.indexOf]
   */
   Macro indexOf(element)
   End;

   /**
      Возвращает индекс последнего элемента равного 'element' или -1, если элемент не найден.

      @Sample [Samples.Collections.List.lastIndexOf]
   */
   Macro lastIndexOf(element)
   End;

   /**
      Возвращает `true`, если коллекция содержит элемент равный 'element' согласно функции 'equalsFun'. Можно не указывать 'equalsFun' если она определена для текущего списка.

      @Sample [Samples.Collections.List.contains]
   */
   Macro contains(element) : Bool
   End;

   /**
      Возвращает `true`, если коллекция содержит все элементы из 'enumerable' согласно функции 'equalsFun'. Можно не указывать 'equalsFun' если она определена для текущего списка.

      @Sample [Samples.Collections.List.containsAll]
   */
   Macro containsAll(enumerable) : Bool
      for(var el, enumerable)
         if (not(contains(el)))
            return false;
         end;
      end;
      return true;
   End;

   /**
      Возвращает `true`, если коллекция отсортирована согласно функции-компаратору 'compareFun'. Можно не указывать 'compareFun' если она определена для текущего списка.
   */
   Macro isSorted(compareFun) : Bool
   End;

   /**
      Сортирует исходный список согласно функции-компаратору 'compareFun' и возвращает ссылку на него. Можно не указывать 'compareFun' если она определена для текущего списка.

      @Sample [Samples.Collections.List.sort]
   */
   Macro sort(compareFun) : ListInterface
   End;

   /**
      Разворачивает исходный список и возращает ссылку на него. ожно не указывать 'compareFun' если она определена для текущего списка.

      @Sample [Samples.Collections.List.reverse]
   */
   Macro reverse() : ListInterface
   End;

   /**
      Возвращает список состоящий из подмножества исходного, начиная с 'from' (включительно), заканчивая 'to' (исключительно).

      @Sample [Samples.Collections.List.subList]
   */
   Macro subList(from : Integer, to : Integer) : ListInterface
   End;

   /**
      Возвращает массив типа [Utils.SArray SArray] состоящий из элементов исходного списка.

      @Sample [Samples.Collections.List.toArray]
   */
   Macro toArray() : SArray
   End;

   /**
      Возвращает максимальное значение коллекции. Можно не указывать '_comparator' если она определена для текущего списка.

      @oparam _comparator ссылка на функцию компаратор.
   */
   Macro max(_comparator) : Variant
      if(ValType(_comparator) == V_UNDEF)
         _comparator = comparator;
      else
         _comparator = Lambdas.getFunc(_comparator);
      end;
      var itr = newIterator();
      if (itr.hasNext() == false)
         return null;
      end;
      var maxV = itr.next();
      while (itr.hasNext())
         var v = itr.next();
         if (ExecMacro2(_comparator, v, maxV) > 0)
            maxV = v;
         end;
      end;
      return maxV;
   End;

   /**
      Возвращает минимальное значение коллекции. Можно не указывать '_comparator' если она определена для текущего списка.
      @oparam _comparator ссылка на функцию компаратор.
   */
   Macro min(_comparator) : Variant
      if(ValType(_comparator) == V_UNDEF)
         _comparator = comparator;
      else
         _comparator = Lambdas.getFunc(_comparator);
      end;;
      var itr = newIterator();
      if (itr.hasNext == false)
         return null;
      end;
      var minV = itr.next();
      while(itr.hasNext())
         var v = itr.next();
         if(ExecMacro2(_comparator, v, minV) < 0)
            minV = v;
         end;
      end;
      return minV;
   End;

   /**
      Возвращает новый обратный итератор.

      @Sample [Samples.Collections.List.newDescIterator]
   */
   Macro newDescIterator(ind) : DescendingIterator
      return DescendingIterator(this, ind);
   End;


   Macro equals(othList)
      if (size != othList.size)
         return false;
      end;
      var itr1 = newIterator();
      var itr2 = othList.newIterator();
      while (itr1.hasnext)
         var t1 = itr1.next();
         var t2 = itr2.next();
         if (ExecMacro2("equalsfunc", t1, t2) == false)
            return false;
         end;
      end;
      return true;
   End;

End;

/**
   Базовый интерфейс для коллекций пар "ключ-значение".

   @see [Collections.HashMap]
   @see [Collections.LinkedHashMap]
   @see [Collections.TreeMap]
   @see [Collections.ArrayMap]
*/
Class (Iterable) MapInterface()

   private var keyComparator;
   private var keyEqualsFunc;
   private var valueComparator;
   private var valueEqualsFunc;

   /**
      Устанавливает функцию-компаратор ключей 'kCompareFun' и возвращает текущую коллекцию.


   */
   Macro setKeyComparator(kCompareFun)
      keyComparator = kCompareFun;
      return this;
   End;

   /**
      Устанавливает функцию проверки на равенство ключей 'kEqualsFun' и возвращает текущую коллекцию.
   */
   Macro setKeyEqualsFunc(kEqualsFun)
      keyEqualsFunc = kEqualsFun;
      return this;
   End;

   /**
      Устанавливает функцию-компаратор значений 'vCompareFun' и возвращает текущую коллекцию.
   */
   Macro setValueComparator(vCompareFun)
      valueComparator = vCompareFun;
      return this;
   End;

   /**
      Устанавливает функцию проверки на равенство значений 'vEqualsFun' и возвращает текущую коллекцию.
   */
   Macro setValueEqualsFunc(vEqualsFun)
      valueEqualsFunc = vEqualsFun;
      return this;
   End;

   /**
      Устанавливает тип ключа 'type' из языка RSL.

      @Sample [Samples.Collections.Map.setKeyType]
   */
   Macro setKeyType(rslType : Integer) : MapInterface
   End;

   /**
      Устанавливает тип значений 'type' из языка RSL.

      @Sample [Samples.Collections.Map.setValueType]
   */
   Macro setValueType(rslType : Integer) : MapInterface
   End;

   /**
      Помещает в коллекцию ключ 'key' и соответсвующее ему значение 'value', если такой ключ уже присутствует, значение меняется, а заменяемое значение возвращается.

      @Sample [Samples.Collections.Map.put]
   */
   Macro put(key, value : Variant) : Variant
   End;

   /**
      Помещает в коллекцию все пары "ключ-значение" из 'map'.

      @Sample [Samples.Collections.Map.putAll]
   */
   Macro putAll(map : MapInterface)
   End;

   /**
      Помещает в коллекцию все аргументы и возвращает текущую карту. Нечетным аргументам соответсвуют ключи, четным - значения.

      @Sample [Samples.Collections.Map.putItems]
   */
   Macro putItems()
      var i, k, v;
      for (i, 1, Parmcount-1, 2)
         GetParm(i, k);
         GetParm(i+1, v);
         put(k, v);
      end;
      return this;
   End;

   /**
      Возвращает значение соответствующее ключу 'key', если такого ключа не найдено возвращает `null`.

      @Sample [Samples.Collections.Map.get]
   */
   Macro get(key) : Variant
   End;

   /**
      Заменяет значение соответствующее ключу 'key' на 'newValue' и возвращает `true`, если ключа не найдено возвращает `false`, при этом добавление нового элемента не происходит.

      @Sample [Samples.Collections.Map.replace]
   */
   Macro replace(key, newValue : Variant) : Bool
   End;

   /**
      Удаляет и коллекции пару с ключем 'key' и возвращает удаляемое значение.

      @Sample [Samples.Collections.Map.remove]
   */
   Macro remove(key) : Variant
   End;

   /**
      Возвращает `true`, если коллекция содержит пару с ключем 'key'.

      @Sample [Samples.Collections.Map.containsKey]
   */
   Macro containsKey(key) : Bool
   End;

   /**
      Возвращает `true`, если коллекция содержит пару со значением равным 'value'

      @Sample [Samples.Collections.Map.containsValue]
   */
   Macro containsValue(value) : Bool
   End;

   /**
      Возвращает массив [Utils.SArray SArray] состоящий из ключей исходной коллекции.

      @Sample [Samples.Collections.Map.keys]
   */
   Macro keys() : SArray
   End;

   /**
      Возвращает массив [Utils.SArray SArray] состоящий из значений исходной коллекции.

      @Sample [Samples.Collections.Map.values]
   */
   Macro values() : SArray
   End;

   /**
      Возвращает новый итератор пар.
   */
   Macro newEntryIterator()
   End;

   /**
      Возвращает новый итератор ключей.
   */
   Macro newKeyIterator()
   End;

   /**
      Возвращает новый итератор значений.
   */
   Macro newValueIterator()
   End;

   Macro newIterator()
      return newEntryIterator();
   End;
End;

/**
   Базовый интерфейс для коллекций уникальных значений.

   @see [Collections.HashSet]
   @see [Collections.TreeSet]
   @see [Collections.LinkedHashSet]
*/
Class (Iterable) SetInterface()

   /**
      Устанавливает необходимые для работы с конкретным 'rslType' типом RSL  'comparator' и 'equalsFunc'

      @Sample [Samples.Collections.set.setType]
   */
   Macro setType(rslType : Integer) : SetInterface

   End;

   /**
      Добавляет 'element' в множество и возвразает `true`, если элемент добавляется впервые.

      @Sample [Samples.Collections.set.add]
   */
   Macro add(element) : Bool
   End;

   /**
      Добавляет все элементы 'enumerable' в конец множества. Возвращает `true` если был добавлен хотя бы один элемент.

      @Sample [Samples.Collections.set.addAll]
   */
   Macro addAll(enumerable) : Bool
   End;

   /**
      Добавляет все аргументы в множество и возвращает текущий экземпляр.

      @Sample [Samples.Collections.set.addItems]
   */
   Macro addItems()
   End;

   /**
      Ищет и удаляет 'element', возвращает `true`, если элемент был найден и удален.

      @Sample [Samples.Collections.set.remove]
   */
   Macro remove(element) : Bool
   End;

   /**
      Ищет и удаляет все элементы иcходного множества, которые содержаться в 'enumerable', который может являться списком, множеством или массивом. Вовзращает `true` если был найден или удален хотя бы один элемент.

      @Sample [Samples.Collections.set.removeAll]
   */
   Macro removeAll(enumerable) : Bool
   End;

   /**
      Возвращает `true`, если сэт содержит элемент равный 'element'.

      @Sample [Samples.Collections.set.contains]
   */
   Macro contains(element) : Bool
   End;

   /**
      Возвращает `true`, если сэт содержит все элементы из 'enumerable'.

      @Sample [Samples.Collections.set.containsAll]
   */
   Macro containsAll(enumerable, equalsFun) : Bool
      for(var el, enumerable)
         if (not(contains(el, equalsFun)))
            return false;
         end;
      end;
      return true;
   End;

   Macro equals(oth)
      if (size != oth.size)
         return false;
      end;
      var itr1 = newIterator();
      var itr2 = oth.newIterator();
      while (itr1.hasnext)
         var t1 = itr1.next();
         var t2 = itr2.next();
         if (ExecMacro2("equalsfunc", t1, t2) == false)
            return false;
         end;
      end;
      return true;
   End;

End;
