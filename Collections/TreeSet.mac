/**
   @package Collections
   Реализация отсортированной коллекции уникальных значений.
*/

import "..\\Mac\\Extra\\Collections\\TreeMap.mac";


Class (SetInterface) TreeSet(_keyComparator)
   size = 0;
   private var inMap = TreeMap(_keyComparator);

   Macro setInMap(newMap)
      inMap = newMap;
   End;

   Macro setType(type : Integer)
      inMap.setKeyType(type);
      return this;
   End;

   Macro add(element : Variant) : Bool
      if (inMap.put(element, PRESENT) != PRESENT)
         size = size + 1;
         return true;
      end;
      return false;
   End;

   Macro addAll(enumerable) : Bool
      var res = false;
      for (var el, enumerable)
         if (add(el))
            res = true;
         end;
      end;
      return res;
   end;

   Macro addItems() : TreeSet
      var i, p;
      for (i, 1, Parmcount-1, 1)
         GetParm(i, p);
         add(p);
      end;
      return this;
   End;

   Macro remove(element : Variant) : Bool
      if (inMap.remove(element) == PRESENT)
         size = size - 1;
         return true;
      end;
      return false;
   End;

   Macro removeAll(enumerable) : Bool
      var res = false;
      for (var el, enumerable)
         if (remove(el))
            res = true;
         end;
      end;
      return res;
   End;

   Macro contains(element : Variant) : Bool
      return (inMap.containsKey(element));
   End;

   Macro containsAll(enumerable) : Bool
      for (var el, enumerable)
         if (contains(el) == false)
            return false;
         end;
      end;
      return true;
   End;

   Macro clear()
      inMap.clear();
      size = 0;
      return this;
   End;

   Macro toArray() : TArray
      return inMap.keys();
   End;

   Macro newInstance() : TreeSet
      var inst = TreeSet();
      inst.setInMap(inMap.newInstance());
      return inst;
   End;

   Macro append(el)
      add(el);
   End;

   Macro newIterator()
      return inMap.newKeyIterator();
   End;
End;
