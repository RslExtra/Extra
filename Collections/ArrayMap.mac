/**
   @package Collections

   Файл включает в себя реализацию интерфейса 'map' через два массива.
   В среднем медленнее чем [Collections.HashMap Hashmap], однако занимает меньше места в памяти.
   Также в качесвте ключей могут выступать только примитивные типы. Отлично подходит для небольшого количества элементов (до 100) и редких операциях удаления, вставки.
*/

import "..\\Mac\\Extra\\Collections\\Iterable.mac";

Private Macro binarySearchMap(arr : TArray, el : Variant) : Integer
   var high = arr.size;
   var low = 0;
   var mid = 0;
   var x;
   var i = -1;
   while(low < high)
      mid = (low + high) / 2;
      x = arr[mid];
      if (el == x)
         return mid;
      elif (el < x)
         high = mid;
         i = -mid-1;
      else
         low = mid + 1;
         i = -low-1;
      end;
   end;
   return i;
End;

Class (MapInterface) ArrayMap()
   size = 0;
   valueComparator = @VariantCompare;
   valueEqualsFunc = @VariantEquals;
   var keysArr = SArray();
   var valuesArr = SArray();

   Macro setKeyType(type : Integer)
      if (type == V_STRING)
         ;
      elif (type == V_INTEGER)
         ;
      elif (type == V_GENOBJ)
         RunError("Объектный тип ключей не допустим.");
      else
         RunError("Данный тип ключей не предусмотрен реализацией." + type);
      end;
      return this;
   End;

   Macro setValueType(type : Integer)
      if (type <= 10)
         valueComparator  = @CF_ComparePrimitive;
         valueEqualsFunc  = @CF_EqualsPrimitive;
      elif (type == V_GENOBJ)
         valueComparator = @CF_CompareToImpl;
         valueEqualsFunc = @CF_EqualsImpl;
      else
         RunError("Данный тип значений не предусмотрен реализацией." + type);
      end;
      return this;
   End;

   Macro put(key, value) : Variant
      var i = binarySearchMap(keysArr, key);
      var oldVal;
      if (i >= 0)
         oldVal = valuesArr[i];
         valuesArr[i] = value;
      else
         i = -i - 1;
         keysArr.add(key, i);
         valuesArr.add(value, i);
         size = size + 1;
      end;
      return oldVal;
   End;

   Macro get(key) : Variant
      var i = binarySearchMap(keysArr, key);
      if (i >= 0)
         return valuesArr[i];
      end;
   End;

   Macro replace(key, value) : Bool
      var i = binarySearchMap(keysArr, key);
      if (i >= 0)
         valuesArr[i] = value;
         return true;
      end;
      return false;
   End;

   Macro remove(key) : Variant
      var i = binarySearchMap(keysArr, key);
      if (i < 0) return; end;
      var remVal = valuesArr.remove(i);
      keysArr.remove(i);
      size = size - 1;
      return remVal;
   End;

   Macro containsKey(key) : Bool
      var i = binarySearchMap(keysArr, key);
      return (i >= 0);
   End;

   Macro containsValue(val)
      return valuesArr.contains(val, valueEqualsFunc);
   End;

   Macro clear()
      keysArr.clear();
      valuesArr.clear();
      size = 0;
   end;

   Macro append(p : Pair)
      put(p.key, p.value);
   End;

   Macro putMapEntries(map) : ArrayMap
      for (var p, map)
         put(p.key, p.value);
      end;
      return this;
   End;

   Macro keys() : SArray
      return keysArr;
   End;

   Macro values() : SArray
      return valuesArr;
   End;

   Macro newIterator()
      return ExecMacro2("ArrayMapEntryIterator", this);
   End;

   Macro newEntryIterator()
      return ExecMacro2("ArrayMapEntryIterator", this);
   End;

   Macro newKeyIterator()
      return ExecMacro2("ArrayMapKeyIterator", this);
   End;

   Macro newValueIterator()
      return ExecMacro2("ArrayMapValueIterator", this);
   End;

End;

Private Class (IteratorInterface) ArrayMapIterator(_arrayMap : ArrayMap)
   var map = _arrayMap;
   var lastReturned = null;
   var nextIndex : Integer = 0;

   Macro getLastReturned()
      return lastReturned;
   End;

   Macro hasNext()
      return (nextIndex != map.size);
   End;

   Macro hasPrev()
      return (nextIndex != 0)
   End;

   Macro next() : Pair
      lastReturned = Pair(map.keysArr[nextIndex], map.valuesArr[nextIndex]);
      nextIndex = nextIndex + 1;
      return lastReturned;
   End;

   Macro prev() : Pair
      nextIndex = nextIndex - 1;
      lastReturned = Pair(map.keysArr[nextIndex], map.valuesArr[nextIndex]);
      return lastReturned;
   End;
End;

Private Macro ArrayMapEntryIterator(map : ArrayMap)
   return ArrayMapIterator(map);
End;

Private Macro ArrayMapKeyIterator(map)
   return map.keysArr.newIterator();
End;

Private Macro ArrayMapValueIterator(map)
   return map.valuesArr.newIterator();
End;
