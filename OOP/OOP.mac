/**
   @Package OOP
   @Ancestor Extra

   Вспомогательные классы и функции для реализации ООП паттернов.
*/
import "..\\Mac\\Extra\\OOP\\Comparators.mac";
import rcw;

/**
   Функция расчета хэшкода типа 'V_STRING'.
*/
Macro StringHashCode(key : String) : Integer
   var h = 7;
   for (var i, 1, StrLen(key), 1)
      h = h * 31 + CodeFor(SubStr(key, i, 1));
   end;
   return abs(h);
End;

/**
   Функция расчета хэшкода типа 'V_INTEGER'.
*/
Macro IntHashCode(i : Integer) : Integer
   return abs(i);
End;

/**
   Функция расчета хэшкода типа 'V_MONEY' , 'V_DOUBLE' или 'V_NUMERIC'.
*/
Macro NumericHashCode(nm) : Integer
   return abs(int(nm * 10000));
End;

/**
   Функция расчета хэшкода типа 'V_DATE'.
*/
Macro DateHashCode(dt : Date) : Integer
   return StringHashCode(String(dt));
End;

/**
   Функция расчета хэшкода типа 'V_TIME'.
*/
Macro TimeHashCode(tm : Time) : Integer
   return StringHashCode(String(tm));
End;

/**
   Функция расчета хэшкода объекта пользовательского класса.
*/
Macro ObjectHashCode(obj : Object)
   var h = ExecMacro2(R2M(obj, "hash"));
   if (ValType(h) == V_INTEGER)
      return h;
   end;
   RunError("\nДля класса " + GenClassName(obj) + " не определена функция hash() : Integer");
End;

/**
   Функция расчета хэшкода произвольного объекта RSL.
*/
Macro VariantHashCode(val : Variant) : Integer
   var type = ValType(val);
   if (type == V_UNDEF)
      return 0;
   elif (type == V_STRING)
      return StringHashCode(val);
   elif (type == V_INTEGER)
      return abs(val);
   elif (type == V_DATE)
      return DateHashCode(val);
   elif (type == V_TIME)
      return TimeHashCode(val);
   elif ((type == V_MONEY) or (type == V_DOUBLE) or (type == V_DOUBLEL) or (type == V_NUMERIC))
      return NumericHashCode(val);
   elif (type == V_GENOBJ)
      if (isEqClass("TARRAY", val))
         return ExecMacro2("TArrayHashCode");
      else
         return ObjectHashCode(val);
      end;
   else
      return 0;
   end;
End;

/**
   Функция расчета хэшкода массива [TArray].
*/
Macro TArrayHashCode(arr) : Integer
   var h = 1;
   for(var el, arr)
      h = h*17 + VariantHashCode(el);
   end;
   return abs(h);
End;

/**
   Вспомогательный класс с реализованными методами [hash], [equals], [toString] и определенным, но не реализованным [compareTo]. Облегчает использование пользовательских классов в пакете [Collections].
*/
Class Any()

   /**
      Возвращает результат сравнения текущего объекта с другим объектом как Integer. Необходимо переопределять в классе-наследнике. При вызове не переопределенного метода будет брошена ошибка.
   */
   Macro compareTo(obj) : Integer
      RunError("Необходимо переопределить метод CompareTo для класса " + GenClassName(this));
      return 0;
   End;

   /**
      Возвращает хэш объекта. В расчете складываются хэши всех примитивных полей приведенных к String. Переопределять при необходимости.
   */
   Macro hash() : Integer
      var h = 0;
      for(var i, 0, GenNumProps(this) - 1, 1)
         var p = this[i];
         if ((ValType(p) != V_UNDEF) and (ValType(p) <= V_TIME))
            h = h + StringHashCode(String(p));
         end;
      end;
      return abs(h);
   End;

   /**
      Возвращает результат сравнения с объектом 'obj'.
   */
   Macro equals(obj) : Bool
      if (ValType(obj) == V_UNDEF)
         return false;
      elif (this == obj)
         return true;
      end;
      return CF_objectEquals(this, obj);
   End;

   /**
      Переопределенная стандартная функция; Возвращает строковое представление текущего объекта как перечисление его свойств и их значений.
   */
   Macro toString() : String
      var res = GenClassName(this) + " [";
      var propnames = GetObjProps(this, true);
      for (var i, 0, propnames.size - 1, 1)
         res = res + propnames[i] + " : " + String(this[i]);
         if (i != propnames.size-1)
            res = res + ", ";
         end;
      end;
      res = res + "]";
      return res;
   End;
End;


/**
   Tupple класс обертка, представлен от Tuple2 до Tuple5. Поля инициализируются конструктором и именуются _1, _2 и т.д.
*/
Class Tuple()
   Macro hash()
      var h = 0;
      for(var i, 0, GenNumProps(this) - 1, 1)
         var p = this[i];
         if ((ValType(p) != V_UNDEF) and (ValType(p) <= V_TIME))
            h = h + StringHashCode(String(p));
         end;
      end;
      return abs(h);
   End;

   Macro equals(tup) : Bool
      return CF_objectEquals(this, tup);
   End;

   Macro toString() : String
      var res = GenClassName(this) + " [";
      var propnames = GetObjProps(this, true);
      for (var i, 0, propnames.size - 1, 1)
         res = res + propnames[i] + " : " + String(this[i]);
         if (i != propnames.size-1)
            res = res + ", ";
         end;
      end;
      res = res + "]";
      return res;
   End;
End;

/**
   Tupple1
*/
Class (Tuple) Tuple1(__1)
   var _1 = __1;
End;

/**
   Tupple2.
*/
Class (Tuple) Tuple2(__1, __2)
   var _1 = __1;
   var _2 = __2;
End;

/**
   Tupple3.
*/
Class (Tuple) Tuple3(__1, __2, __3)
   var _1 = __1;
   var _2 = __2;
   var _3 = __3;
End;

/**
   Tupple4.
*/
Class (Tuple) Tuple4(__1, __2, __3, __4)
   var _1 = __1;
   var _2 = __2;
   var _3 = __3;
   var _4 = __4;
End;

/**
   Tupple5.
*/
Class (Tuple) Tuple5(__1, __2, __3, __4, __5)
   var _1 = __1;
   var _2 = __2;
   var _3 = __3;
   var _4 = __4;
   var _5 = __5;
End;