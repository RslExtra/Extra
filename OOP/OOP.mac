/**
   @Package OOP

   Вспомогательные классы и функции для реализации ООП паттернов.
*/
import "..\\Mac\\Extra\\Utils\\Comparators.mac";
import rcw;

/**
   Функция расчета хэшкода типа 'V_STRING'.
*/
Macro StringHashCode(key : String) : Integer
   var h = 7;
   for (var i, 1, StrLen(key), 1)
      h = h * 31 + CodeFor(SubStr(key, i, 1));
   end;
   return abs(h);
End;

/**
   Функция расчета хэшкода типа 'V_INTEGER'.
*/
Macro IntHashCode(i : Integer) : Integer
   return abs(i);
End;

/**
   Функция расчета хэшкода типа 'V_MONEY' , 'V_DOUBLE' или 'V_NUMERIC'.
*/
Macro NumericHashCode(nm) : Integer
   return abs(int(nm * 10000));
End;

/**
   Функция расчета хэшкода типа 'V_DATE'.
*/
Macro DateHashCode(dt : Date) : Integer
   return StringHashCode(String(dt));
End;

/**
   Функция расчета хэшкода типа 'V_TIME'.
*/
Macro TimeHashCode(tm : Time) : Integer
   return StringHashCode(String(tm));
End;

/**
   Функция расчета хэшкода объекта пользовательского класса.
*/
Macro ObjectHashCode(obj : Object)
   var h = ExecMacro2(R2M(obj, "hash"));
   if (ValType(h) == V_INTEGER)
      return h;
   end;
   RunError("\nДля класса " + GenClassName(obj) + " не определена функция hash() : Integer");
End;

/**
   Функция расчета хэшкода произвольного объекта RSL.
*/
Macro VariantHashCode(val : Variant) : Integer
   var type = ValType(val);
   if (type == V_UNDEF)
      return 0;
   elif (type == V_STRING)
      return StringHashCode(val);
   elif (type == V_INTEGER)
      return abs(val);
   elif (type == V_DATE)
      return DateHashCode(val);
   elif (type == V_TIME)
      return TimeHashCode(val);
   elif ((type == V_MONEY) or (type == V_DOUBLE) or (type == V_DOUBLEL) or (type == V_NUMERIC))
      return NumericHashCode(val);
   elif (type == V_GENOBJ)
      if (isEqClass("TARRAY", val))
         return ExecMacro2("TArrayHashCode");
      else
         return ObjectHashCode(val);
      end;
   else
      return 0;
   end;
End;

/**
   Функция расчета хэшкода массива [TArray].
*/
Macro TArrayHashCode(arr) : Integer
   var h = 1;
   for(var el, arr)
      h = h*17 + VariantHashCode(el);
   end;
   return abs(h);
End;
/**
   Универсальная функция проверки на равенство двух переменных языка RSL 'o1' и 'o2'.

   Функция работает по следующему принципу:
   - Если аргументы разных типов вернет `false`,
   - Если одного примитивного типа, пернет результат сравнения через `==`.
   - Для объектных аргументов, попробует вызвать метод `equals(obj) : Bool`, а затем если не будет найден 'compareTo'. Если ни один из методов не определен бросит ошибку.
   - Если аргументы равны `null`, вернет `false`
   - Если аргументы типа [TArray] или наследного класса, вернет результат функции [TArrayEquals].

*/
Macro VariantEquals(o1: Variant, o2: Variant) : Bool
   var t1 = ValType(o1);
   var t2 = ValType(o2);
   if (t1 != t2) return false; end;
   if (t1 <= 10)                    // Примитивы
      return (o1 == o2);
   elif (t1 == V_GENOBJ)            // Объекты
      if (isEqClass("TArray", o1))
         return ExecMacro2("TArrayEquals", o1, o2);
      end;
      var rEq = ExecMacro2(R2M(o1,"equals"), o2);
      if (ValType(req) == V_BOOL) return req; end;
      var rCmp = ExecMacro2(R2M(o1,"compareTo"), o2);
      if (ValType(rCmp) == V_INTEGER)
         return (rCmp == 0);
      end;
      RunError("\nДля класса " + GenClassName(o1) + " не определена функция Equals(otherObj) : Bool");
   end;
   return (o1 == o2);
End;

/**
   Функция эквивалентности двух массивов [TArray]. Последовательно вызывает для каждой пары элементов массивов [VariantEquals].
   Если в результате сравнеия хоть одна пара не эквивалентна, вернет `false`.
*/
Macro TArrayEquals(arr1, arr2) : Bool
   if (arr1.size != arr2.size) return false; end;
   for(var i, 0, arr1.size - 1, 1)
      if (VariantEquals(arr1[i], arr2[i]) == false)
         return false;
      end;
   end;
   return true;
End;

/**
   Универсальная функция-компаратор для двух переменных RSL 'o1' и 'o2'. Для двух примитивов одного типа проведет сравнение через `> < ==`, если примитивы будут разного типа бросит ошибку. Для разных типов вернет результат сравнения числовых констант типов, т.е по возрастанию будут идти (Undefined, Integer, String и т.д ).Для объектных типов попробует выполнить метод `compareTo(obj) : Integer`, если такой метод не будет определен в классе, бросит ошибку. Если оба элемента равны `null` вернет 0. Работает медленней чем собственные функции-компараторы, но за константное время, т.е. не усложняет алгоритм использующий эту функцию.
*/
Macro VariantCompare(o1: Variant, o2: Variant) : Integer
   var t1 = ValType(o1);
   var t2 = ValType(o2);
   var sign = 1;
   if (t1 != t2)
      return (t1-t2);
   end;

   if (t1 == V_UNDEF)
      return 0;
   elif (t1 <= V_TIME)                    // Примитивы
      return CF_ComparePrimitive(o1, o2);
   elif (t1 == V_GENOBJ)            // Объекты
      var rCmp = ExecMacro2(R2M(o1,"compareTo"), o2);
      if (ValType(rCmp) == V_INTEGER)
         return sign*rCmp;
      end;
      RunError("\nДля класса " + GenClassName(o1) + " не определена функция CompareTo(otherObj) : Integer");
   end;
   return CF_ComparePrimitive(o1, o2);
End;

/**
   Вспомогательный класс с реализованными методами [hash], [equals], [toString] и определенным, но не реализованным [compareTo]. Облегчает использование пользовательсих классов в пакете [Collections].
*/
Class Any()
   /**
      Возвращает результат сравнения текущего объекта с другим объектом как Integer. Необходимо переопределять в классе-наследнике. При вызове не переопределенного метода будет брошена ошибка.
   */
   Macro compareTo(obj) : Integer
      RunError("Необходимо переопределить метод CompareTo для класса " + GenClassName(this));
      return 0;
   End;

   /**
      Возвращает хэш объекта. В расчете складываются хэши всех примитивных полей приведенных к String. Переопределять при необходимости.
   */
   Macro hash() : Integer
      var h = 0;
      for(var i, 0, GenNumProps(this) - 1, 1)
         var p = this[i];
         if ((ValType(p) != V_UNDEF) and (ValType(p) <= V_TIME))
            h = h + StringHashCode(String(p));
         end;
      end;
      return abs(h);
   End;

   /**
      Возвращает результат сравнения с объектом 'obj'.
   */
   Macro equals(obj) : Bool
      if (ValType(obj) == V_UNDEF)
         return false;
      elif (this == obj)
         return true;
      end;
      return CF_objectEquals(this, obj);
   End;

   /**
      Возвращает строковое представление текущего объекта как перечисление его свойств и их значений.
   */
   Macro toString() : String
      var res = GenClassName(this) + " [";
      var propnames = GetObjProps(this, true);
      for (var i, 0, propnames.size - 1, 1)
         res = res + propnames[i] + " : " + String(this[i]);
         if (i != propnames.size-1)
            res = res + ", ";
         end;
      end;
      res = res + "]";
      return res;
   End;
End;