import "..\\mac\\extra\\OOP\\Exceptions.mac";
//<
    var resultSqr = null;

   // Пример функции возведения Integer в квадрат
   Macro mysqr(p)
      if (ValType(p) != V_INTEGER)
         return Throw(IllegalArgumentException("Не верный тип " + p));
      end;
      resultSqr = p * p;
      if (resultSqr < 0)  // В случае переполнения инта
         RunError("Переполнение Integer");
      end;
   End;

   Macro doOnIllegal(ex)
      println("I'm on IllegalArgumentException");
   End;

   Macro doOnError(er)
      println("I'm on error");
   End;

   Macro finallyBlock()
      println("i'm in finnaly block.");
      println("Result is " + resultSqr);
   End;

   Macro trySqr(parm)
      try(@mysqr, parm)                                     // Передаем фукнцию по ссылке и параметры
         .catch(IllegalArgumentException, @doOnIllegal)     // Если иключение того же типа или типа потомка, выполняется блок doOnIllegal
         .catch(0, @doOnError)                              // Обработает любую ошибку
         .finally(@finallyBlock);                           // В конце выполняем блок
   End;

   trySqr(25);            // Исключений и ошибок не будет, выполнится лишь блок finally
   trySqr("1");           // Не верный тип аргумента
   trySqr(12000000);      // Будет ошибка переполнения
//>
