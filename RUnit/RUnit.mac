/**
   @Package RUnit
   @Title   Юнит-тестирование
   Библиотека для легкого тестирования ваших классов, макросов и модулей. Использует рефлексию, поэтому для создания тестов, необходимо следовать  простым шагам:
   - Создаём отдельный макрофайл.
   - Импортируем библиотеку `import "..\\mac\\extra\\RUnit\\RUnit.Mac";`
   - Если тестируется исполняемый модуль, то в тексте него после иннициализации глобных переменных и до начала исполнения необходимо добавить строку
     `if (ExecMacro2("UnitTestMarker") != null) return; end;`, что завершит выполнение модуля, при его импорте.
   - Импортируем тестируемый модуль
   - При необходимости настраиваем поведение RUnit, через объект [RUnitTest].

   - Создаем тестовый кейс как класс, расширяющий 'TestCase'.
   - Тестовые методы называем начиная со слова `test_`
   - Все остальные вспомогательные методы и поля класса называем иначе.
   - В конце файла создаем классы
   - При сборке мусора, т.е следующей строкой после объявления класса начнется выполнение тестов.
*/

import rcw;

/**
   Объект класса [RUnitProperties] для настройки поведения тестов.
*/
var RUnitTest = GenObject("RUnitProperties");

Private var AllTestResult = GenObject("AllTestsResult");

Private Class ModuleMarker()
   Macro Destructor()
      debugbreak;
      var a = getCallStack;
      println(1);
   End;
End;

Macro getTimeDiffMillis(t1, t2) : Integer
   var strTime = String((t2 - t1) : m);
   var h = Int(SubStr(strTime, 1, 2));
   var m = Int(SubStr(strTime, 4, 2));
   var s = Int(SubStr(strTime, 7, 2));
   var ms = Int(SubStr(strTime, 10, 2));
   return ((((h*60) + m) * 60) + s) * 100 + ms;
end;

Private Class RUnitProperties()
   /** Флаг запуска теста. При установка значения `false`, тесты запускаться не будут. */
   var RUN_UNIT_TEST               : Bool = true;           // Запускать тест

   /** Флаг вывода успешнопройденных тестовых кейсов. По умолчанию `true` */
   var SHOW_SUCCEED_CASE       : Bool = true;

   /** Флаг вывода успешнопройденных тестовых функций. По умолчанию `true` */
   var SHOW_SUCCEED_FUN        : Bool = true;

   /** Флаг вывода успешнопройденных ассертов. По умолчанию `false` */
   var SHOW_SUCCEED_ASSERT     : Bool = false;

   /** Флаг вывода времени исполнения кейса */
   var SHOW_CASE_EXECUTION_TIME     : Bool = true;

   /** Флаг вывода времени исполнения тестового метода */
   var SHOW_FUN_EXECUTION_TIME      : Bool = true;
   /** Флаг записи в логгер. По умолчанию `false` при установке этого флага в `true`, так же необходимо установить экземпляр логгера в поле
   [RUnitProperties.LOGGER_INSTANCE LOGGER_INSTANCE] */
   var WRITE_TO_LOG                : Bool = false;          // Писать в текстовый лог

   /** Флаг записи в лог успешнопройденных тестовых кейсов. По умолчанию `true` */
   var LOG_SUCCEED_CASE       : Bool = true;

   /** Флаг записи в лог успешнопройденных тестовых функций. По умолчанию `true` */
   var LOG_SUCCEED_FUN        : Bool = true;

   /** Флаг записи в лог успешнопройденных ассертов. По умолчанию `false` */
   var LOG_SUCCEED_ASSERT     : Bool = false;

   /** Флаг записи в лог времени исполнения */
   var LOG_EXECUTION_TIME     : Bool = true;
   /** Экземпляр класса логера, логер должен иметь методы 'i()' для записи успешно пройденных тестов, и 'e()' для проваленных. Можно воспользоваться одним из логеров [LogR.SingleLogger] или [LogR.Logger] */
   var LOGGER_INSTANCE                    = null;           // Экземпляр логгера

   /** Установка префикса для тестовых методов. По умолчанию 'TEST_'.*/
   var METHOD_PREFIX : String = "TEST_";             // Префикс тестовых методов
End;

/**
   Генерирует массив [TArray] из переданных параметров.
   @oparam varargs параметры инициализации массива.
*/
Macro Arr_()
   var arr = TArray();
   var p, i = 0;
   while(i < Parmcount)
      getParm(i, p);
      arr[i] = p;
      i = i + 1;
   end;
   return arr;
End;

Private Macro toLogI(msg)
   RUnitTest.LOGGER_INSTANCE.i(msg);
End;

Private Macro toLogE(msg)
   RUnitTest.LOGGER_INSTANCE.e(msg);
End;

Private Class AssertResult(_definition : String)
   var definition = _definition;
   var passed : Bool = true;
   var msg = "";
   var num = 0;

   Macro toScreen()
      if (not(RUnitTest.SHOW_SUCCEED_ASSERT) and passed) return; end;
      var st = "ok  ";
      if (not(passed))
         st = "fail";
      end;
      println("      " + String(num:2:0) + " : " + st + " : " + " - "+ definition + " - " + msg);
   End;

   Macro toLog()
      if (RUnitTest.LOG_SUCCEED_ASSERT and passed) return; end;
      if (not(passed))
         toLogE("fail - " + definition + " - " + msg);
      else
         toLogI("ok   - " + definition + " - " + msg);
      end;
   End;
End;

Private Class FunResult(_funcname)
   var funcname       : String  = _funcname;
   var passed         : Bool    = true;
   var pass_asserts   : Integer = 0;
   var fail_asserts   : Integer = 0;
   var execTimeMillis : Integer = 0;
   var fail_flag      : Bool    = false;
   var fail_msg       : String  = "";

   var assertResults = TArray();

   Macro addAssertResult(assRes : AssertResult)
      assertResults[assertResults.size] = assRes;
      if (assRes.passed)
         pass_asserts = pass_asserts  + 1;
      else
         fail_asserts = fail_asserts + 1;
         passed = false;
      end;
   End;

   Macro toScreen()
      if (not(RUnitTest.SHOW_SUCCEED_FUN) and passed) return; end;

      var st = "[OK]";
      if (not(passed))
         st = "[FAIL]";
      end;
      var stats = "   Asserts run:" + (pass_asserts + fail_asserts) + ", failed:" + fail_asserts;
      print("   "); print(funcname:20:0); print(st:8:0); println(stats);
      if (RUnitTest.SHOW_FUN_EXECUTION_TIME)
         println("   " + "(" + execTimeMillis + " ms)");
      end;
      for(var assRes, assertResults)
         assRes.toScreen();
      end;
      println("");
   End;

   Macro toLog()
      if (RUnitTest.LOG_SUCCEED_FUN and passed) return; end;
      var stats = "   Asserts run:" + (pass_asserts + fail_asserts) + ", failed:" + fail_asserts;
      if (not(passed))
         toLogE(String(funcname:20:0) + "[FAIL]  " + stats);
      else
         toLogI(String(funcname:20:0) + "[OK]    " + stats);
      end;

      for(var assRes, assertResults)
         assRes.toLog();
      end;
   End;
End;

Private Class CaseResult(_casename : String)
   var casename       : String  = _casename;
   var passed         : Bool    = true;
   var pass_funs      : Integer = 0;
   var fail_funs      : Integer = 0;
   var execTimeMillis : Integer = 0;
   var funResults = TArray();

   Macro addFunResult(_funResult : FunResult)
      funResults[funResults.size] = _funResult;
      if(_funResult.passed)
         pass_funs = pass_funs + 1;
      else
         fail_funs = fail_funs + 1;
         passed = false;
      end;
   End;

   Macro toScreen()
      if (funResults.size == 0) return; end;
      if (not(RUnitTest.SHOW_SUCCEED_CASE) and passed) return; end;

      var st = "[OK]";
      if (not(passed))
         st = "[FAIL]";
      end;
      var stats = "Tests method run: " + (pass_funs + fail_funs) + ", failed: " + fail_funs;

      print(casename:20:0); println(st:10:0);
      println(stats);
      if (RUnitTest.SHOW_CASE_EXECUTION_TIME)
         println("("+ execTimeMillis +" ms)")
      end;
      println("");

      for (var funRes, funResults)
         funRes.toScreen();
      end;
      println("--------------------------------------------------------------------------------");

   End;

   Macro toLog()
      if(funResults.size == 0) return; end;
      if(RUnitTest.LOG_SUCCEED_CASE and passed) return; end;
      var stats = "Tests method run: " + (pass_funs + fail_funs) + ", failed: " + fail_funs;
      if(not(passed))
         toLogE(String(casename:20:0) + "[FAIL]");
      else
         toLogI(String(casename:20:0) + "[OK]");
      end;
      toLogI(stats);
      for(var funRes, funResults)
         funRes.toLog();
      end;
   End;
End;

Private Class AllTestsResult()
   var passed = true;
   var pass_case = 0;
   var fail_case = 0;
   var printed = false;
   var testResults = TArray();

   Macro addTestResult(testResult : caseResult)
      testResults[testResults.size] = testResult;
      if(testResult.passed)
         pass_case = pass_case + 1;
      else
         fail_case = fail_case + 1;
         passed = false;
      end;
   End;

   Macro printTestResults()
      if(testResults.size == 0) return; end;

      Println("================================= START TESTS ==================================");
      var st = "[OK]";
      if(not(passed))
         st = "[FAILED]"
      end;
      println(st + " Testcases successful complete: " + pass_case + "/" + (pass_case+fail_case));
      println("");
      for(var testRes, testResults)
         testRes.toScreen();
      end;
      //Println("================================== END TESTS ==========+========================");
   End;

   Macro logTestResults()
      if(testResults.size == 0) return; end;
      if(not(RUnitTest.WRITE_TO_LOG)) return; end;

      toLogI("START UNIT TESTS");
      var st;;
      if(not(passed))
         st = "[FAILED]";
         toLogE("[FAILED] " + "Testcases successful complete: "  + pass_case + "/" + (pass_case+fail_case));
      else
         toLogI("[OK    ] " + "Testcases successful complete: "  + pass_case + "/" + (pass_case+fail_case));
      end;
      for(var testRes, testResults)
         testRes.toLog();
      end;
      toLogI("END UNIT TESTS");

   End;

   Macro Destructor()
      if (not(printed))
         printTestResults();
         logTestResults();
         printed = true;
      end;
   End;
End;

Private Class AssertClass(_val1, _val2)
   var val1 = _val1;
   var val2 = _val2;
   private var strAssert = StrSubst(GenClassName(this),"CLASS", "") + " ( " + String(_val1) + " , " + String(_val2) + " )";
   var result = AssertResult(strAssert);
   var num = 0;

   Macro setNum(n : Integer)
      num = n;
      result.num = n;
   End;

   Macro runAssert() : Bool
      return true;
   End;

   Macro setResult(res : Bool)
      result.passed = res;
   End;

   Macro getResult() : Bool
      return true;
   End;

   Macro setFaultCause(cause : String)
      result.msg = cause;
   End;

End;

Private Class (AssertClass) AssertEqualsClass(_val1, _val2)
   initAssertClass(_val1, _val2);

   Macro runAssert() : Bool
      if(ValType(val1) != ValType(val2))
         setResult(false);
         setFaultCause("AssertEquals different ValTypes.");
      elif (val1 != val2)
         setResult(false);
         setFaultCause("AssertEquals expected value: " + String(val1) + " , real value: " + String(val2));
      else
         setResult(true);
      end;
   End;
End;

Private Class (AssertClass) AssertNullClass(_val1)
   initAssertClass(_val1);

   Macro runAssert() : Bool
      if(ValType(val1) == V_UNDEF)
         setResult(true);
      else
         setResult(false);
         setFaultCause("AssertNull expected null-value, but value is: " + String(val1));
      end;
   End;
End;

Private Class (AssertClass) AssertNotNullClass(_val1)
   initAssertClass(_val1);

   Macro runAssert() : Bool
      if(ValType(val1) != V_UNDEF)
         setResult(true);
      else
         setResult(false);
         setFaultCause("AssertNotNull expected notnull-value, but value is null");
      end;
   End;
End;

Private Class (AssertClass) AssertTrueClass(_val1)
   initAssertClass(_val1);

   Macro runAssert() : Bool
      if(ValType(val1) == V_BOOL)
         if(val1)
            setResult(true);
         else
            setResult(false);
            setFaultCause("AssertTrue expected TRUE, but value is: " + String(val1));
         end;
      else
         setResult(false);
         setFaultCause("AssertTrue value is not BOOL type: " + String(val1));
      end;
   End;
End;

Private Class (AssertClass) AssertFalseClass(_val1)
   initAssertClass(_val1);

   Macro runAssert() : Bool
      if(ValType(val1) == V_BOOL)
         if(Not(val1))
            setResult(true);
         else
            setResult(false);
            setFaultCause("AssertFalse expected FALSE, but value is: " + String(val1));
         end;
      else
         setResult(false);
         setFaultCause("AssertFalse value is not BOOL type: " + String(val1));
      end;
   End;
End;

Private Class (AssertClass) AssertArraysClass(_arr1, _arr2)
   initAssertClass(_arr1, _arr2);

   Macro runAssert() : Bool
      setResult(true);
      if(IsEqClass("TArray", val1) == false)
         setResult(false);
         setFaultCause("AssertArrays first parm is not an Array");
      elif(IsEqClass("TArray", val2) == false)
         setResult(false);
         setFaultCause("AssertArrays second parm is not an Array");
      elif(val1.size != val2.size)
         setResult(false);
         setFaultCause("AssertArrays arrays has a different size. " + val1.size + " and " + val2.size);
      else
         var i;
         for(i,0,val1.size-1,1)
            if(val1[i]!=val2[i])
               setResult(false);
               setFaultCause("AssertArrays arrays are not equal. At least on index: " + i + " , values: '" + val1[i] + "' and '" + val2[i] + "'");
               break;
            end;
         end;
      end;
   End;
End;

Private Class (AssertClass) AssertErrorClass(_expCode, _err)
   initAssertClass(_expCode, _err);

   if(isEqClass("TRSLERROR",_err))
      strAssert = StrSubst(GenClassName(this),"CLASS", "") + " ( " + String(_expCode) + " , " + String(_err.code) + " )";
      result = AssertResult(strAssert);
   end;

   Macro runAssert()
      var expCode = val1;
      var err = val2;
      if(ValType(err)!=V_UNDEF)
         if(ValType(expCode) == V_INTEGER)    // Если ожидалась ошибка проверим код
               if((expCode == -1) or (expCode == err.Code))
                  setResult(true);
               else
                  setResult(false);
                  setFaultCause("Expected error with code: " + expCode + " , but was catched error with code: " + err.code + " - " + err.Message);
               end;
         else
            setResult(false);
            setFaultCause("Unexpected Error. Code: " + err.code + " - " + err.Message);
         end;
      else
         setResult(false);
         setFaultCause("AssertError expected ERROR, but wasn't catched any error. Value is:" + val1);
      end;
   End;
End;

Private Class (AssertClass) AssertExceptionClass(_val1, _val2)
   initAssertClass(_val1, _val2);

   Macro getException(val1, cand)
      if (isEqClass("Exception", cand) == false)
         var lastExc = ExecMacro2("GetLastException");
         if (isEqClass("Exception", lastExc))
            val2 = lastExc;
         end;
      end;
   End;

   Macro runAssert() : Bool
      getException(val1, val2);
      if (isEqClass("Exception", val1) == false)
         setResult(false);
         setFaultCause("Expected value is not [Exception] class type.");
      elif (isEqClass("Exception", val2) == false)
         setResult(false);
         setFaultCause("Real value is not [Exception] class type.");
      elif (isEqClass(GenClassName(val1), val2) == false)
         setResult(false);
         setFaultCause("AssertException expected Exception: " + GenClassName(val1) + ", but real: " + GenClassName(val2));
      else
         setResult(true);
      end;
   End;
End;


Private Class UnitFun(_testCase, _funcname : String)
   var case = _testCase;
   var funcname = _funcname;
   var expErrCode;
   var result    = FunResult(_funcname);
   private var curAssNum = 0;

   Macro setExecTime(millis : Integer)
      result.execTimeMillis = millis;
   End;

   Macro runTestFun()
      var curTest = 0;
      var t0 = time();
      var res = GenRun(case, funcname);
      setExecTime(getTimeDiffMillis(t0, time));

      OnError(err) // При ошибке внутри функции
         var errAss = AssertErrorClass(expErrCode, err);
         this.addAssert(errAss);
   End;

   Macro addAssert(ass)
      if (result.fail_flag) return; end;
      curAssNum = curAssNum + 1;
      ass.setNum(curAssNum);
      ass.RunAssert();
      result.addAssertResult(ass.result);
   End;

   Macro fail(msg : String)
      result.fail_flag = true;
      result.fail_msg  = msg;
   End;
End;

/**

*/
Class TestCase(_name)
   var name       : String;
   var testFuns   : TArray;
   var curTestFun : UnitFun;

   var complete   : Bool;

   var result;

   if(ValType(_name) == V_STRING)
      name = _name;
   end;

   Macro beforeClass()
   End;

   Macro afterClass()
   End;

   Macro beforeTest()
   End;

   Macro afterTest()
   End;

   Macro assertEquals(val1, val2)
      curTestFun.addAssert(AssertEqualsClass(val1,val2));
   End;

   Macro assertNull(val1) : Bool
      curTestFun.addAssert(AssertNullClass(val1));
   End;

   Macro assertNotNull(val1) : Bool
      curTestFun.addAssert(AssertNotNullClass(val1));
   End;

   Macro assertTrue(val1, val2) : Bool
      curTestFun.addAssert(AssertTrueClass(val1));
   End;

   Macro assertFalse(val1) : Bool
      curTestFun.addAssert(AssertFalseClass(val1));
   End;

   Macro assertError(val1) : Bool
      curTestFun.addAssert(assertErrorClass(val1));
   End;

   Macro assertException(val1, val2)
      curTestFun.addAssert(AssertExceptionClass(val1,val2));
   End;

   Macro assertArrays(arr1, arr2)
      curTestFun.addAssert(assertArraysClass(arr1, arr2));
   End;

   Macro fail(msg : String)
      curTestFun.fail(msg);
   End;

   Macro catchError(_code)
      var code = _code;
      if(valType(code) != V_INTEGER)
         code = -1;           // Catch all errors
      end;
      curTestFun.expErrCode = code;
   End;

   Private Macro __AddTestFun(funName)
      testFuns[testFuns.size] = UnitFun(this, funName);
   End;

   Private Macro __ParseMethods()
      for(var mn, GetObjMethods(this))
         if(SubStr(mn,1,5) == StrUpr(RUnitTest.METHOD_PREFIX))
            __AddTestFun(mn);
         end;
      end;
   End;

   Macro startTests()
      testFuns = TArray();
      __ParseMethods();

      for(curTestFun, testFuns)
         beforeTest();
         curTestFun.runTestFun();
         afterTest();
         result.addFunResult(curTestFun.result);
      end;
   End;

   Macro RunCase()
      if(ValType(name) == V_UNDEF)
         name = GenClassName(this);
      end;
      result = CaseResult(name);
      var t0 = time();
      beforeClass();
      startTests();
      afterClass();
      result.execTimeMillis = getTimeDiffMillis(t0, time);
      AllTestResult.addTestResult(result);
   End;

   Macro Destructor()
      if((ValType(complete) == V_UNDEF) and RUnitTest.RUN_UNIT_TEST)
         RunCase();
         complete = true;
      end;
   End;
End;

Macro UnitTestMarker()
   return ModuleMarker();
End;

Macro UT_PrintResults()
   AllTestResult.printTestResults();
End;

Macro UT_LogResults()
   AllTestResult.logTestResults();
End;