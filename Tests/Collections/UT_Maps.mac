import "..\\mac\\extra\\RUnit\\RUnit.Mac";
import "..\\mac\\extra\\Collections\\Collections.Mac";

RUnitTest.SHOW_SUCCEED_FUN = false;

Private Class (Any) Account(_acc : String, _summ : Integer)
   var acc = _acc;
   var summ = _summ;

   Macro compareTo(othAcc : Account)
      if(acc > othAcc.acc)
         return 1;
      elif(acc < othAcc.acc)
         return -1;
      else
         if(summ > othAcc.summ)
            return 1;
         elif (summ > othAcc.summ)
            return -1;
         else
            return 0;
         end;
      end;
   End;
End;

Class (TestCase) TestMap()
   var map;
   var objMap;

   Macro beforeTest()

   End;

   Macro afterTest()
      map = null;
      objMap = null
   End;

   Macro test_put_get()
      map.put("key1", 1);
      assertEquals(map.size, 1);

      assertTrue(map.containsValue(1));
      assertEquals(map.get("key1"), 1);
      assertNull(map.get("SomeKey"));

      map.put("key2", 2);
      assertEquals(map.size, 2);

      var r = map.put("key1", 111);
      assertEquals(r , 1);
      assertEquals(map.get("key1"),111);
      assertEquals(map.size, 2);

      map.clear();
      assertEquals(map.size, 0);
      assertTrue(map.isEmpty);

      var i;
      for(i, 1, 1000)
         map.put("key" + i, "val" + i);
      end;

      assertEquals(map.size, 1000);
      var finded = 0;
      var v;
      for(i, 1, 1000)
         v = map.get("key" + i);
         if(ValType(v) != V_UNDEF)
            finded = finded + 1;;
         end;
      end;
      assertEquals(finded, 1000);
   End;

   Macro test_putMapEntries()
      var addedMap = ArrayMap();
      for(var i, 1, 100)
         addedMap.put("kk" + i, "vv" + i);
      end;
      map.putMapEntries(addedMap);
      assertEquals(100, map.size);

      for ( i, 1, 100)
         if (map.containsKey("kk" + i) == false)
            fail("Map doesn't contain key kk" + i);
         end;
      end;
   End;

   Macro test_replace()
      map.put("k1", 1);
      map.put("k2", 2);
      var r = map.replace("k3", 3);
      assertFalse(r);
      assertEquals(2, map.size);
      r = map.replace("k1", 10);
      assertTrue(r);
      assertEquals(10, map.get("k1"));
   End;

   Macro test_toArrays()
      var i;
      var arr;
      for(i, 1, 1000)
         map.put("key" + i, "val" + i);
      end;
      arr = map.keys();
      assertEquals(arr.size, 1000);
      arr = map.values();
      assertEquals(arr.size, 1000);
   End;

   Macro test_remove()
      var i;
      for(i, 1, 1000)
         map.put("key" + i, "val" + i);
      end;

      assertEquals(map.size, 1000);
      var v = map.remove("key566");
      assertEquals(v, "val566");

      assertEquals(map.size, 999);
      assertTrue(map.containsValue("val566") == false);
      v = map.remove("key566");
      assertNull(v);
      v = map.remove("key1");
      assertEquals(v, "val1");
      assertEquals(map.size, 998);
      var removed = 0;

      for(i, 1, 1000)
         v = map.remove("key" + i);
         if(ValType(v) != V_UNDEF)
            removed = removed + 1;
         end;
      end;
      assertEquals(removed, 998);
      assertEquals(map.size, 0);
      assertTrue(map.isEmpty);
   End;
End;


Class (TestMap) TestHashMap()
   Macro beforeTest()
      map = HashMap();
      objMap = ObjectHashMap();
   End;
End;

Class (TestMap) TestLinkedHashMap()

   Macro beforeTest()
      map = LinkedHashMap();
   End;

   Macro test_order()
      map.put(1, 1);
      map.put(2, 2);
      map.put(3, 3);
      assertArrays(arr_(1, 2, 3), map.keys());
      assertArrays(arr_(1, 2, 3), map.values());
   End;
End;

Class (TestMap) TestSortedLinkdeHashMap()

   Macro beforeTest()
      map = SortedLinkedHashMap();
   end;

   Macro test_order()
      map.put(3, 3);
      map.put(0, 0);
      map.put(2, 2);
      map.put(1, 1);
      assertArrays(arr_(0, 1, 2, 3), map.keys());
      assertArrays(arr_(0, 1, 2, 3), map.values());
      map.put(2, 20);
      assertArrays(arr_(0, 1, 2, 3), map.keys());
      assertArrays(arr_(0, 1, 20, 3), map.values());
   End;
End;

Class (TestMap) TestTreeMap()
   Macro beforeTest()
      map = TreeMap();
      objmap = TreeMap();
   End;
End;

Class (TestMap) TestArrayMap()
   Macro beforeTest()
      map = ArrayMap();
   End;
End;

TestHashMap;
TestLinkedHashMap;
TestSortedLinkdeHashMap;
TestTreeMap;
TestArrayMap;