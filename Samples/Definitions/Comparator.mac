
Private Class Person(_name, _age, _capital)
    var name : String = _name;
    var age  : Integer = _age;
    var capital : Money = _capital;

    Macro toString() : String
        return name + " (" + age + ") состояние : " + capital;
    End;
End;

/*
    Сравниваем сначала по возрасту, затем по капиталу.
*/
Macro SampleComparator(p1, p2)
    if (p1.age > p2.age)
        return 1;
    elif (p1.age < p2.age)
        return -1;
    else
        if (p1.capital > p2.capital)
            return 1;
        elif (p1.capital < p2.capital)
            return -1;
        else
            return 0;
        end;
    end;
End;

var arr = TArray();

arr[0] = Person("Mike", 48, $200);
arr[1] = Person("Alice", 21, $150000);
arr[2] = Person("Gendalf", 88, $0);
arr[3] = Person("Bob", 21, $500);

arr.sort(@SampleComparator); // Отсортируем массив используя наш компаратор, передаем используя @
for(var p, arr)
    println(p);              // Выведем результат на экран
end;